#break-room
(office interior *)
(name *) Break Room
(look *)
    Shadows and grime linger in dark corners of this dismal room with just one tiny frosted window. 
    Some rotting picnic tables strewn with layers of sand and crumbling trash are pushed against one wall, and 
    (if) (#cage is open)
    (then)
        an open
    (else)
        a closed
    (endif)
    wire cage
    (collect items in #cage to $Items)
    (if) ~(empty $Items)
    (then)
        , in which (is $Items) (a $Items)
    (endif)
    .
    (par)
    Open doorways lead west and north.
(place #storage-room north of *)
(#cage/
    #tiny-frosted-window/
    #tables/
    #crumbling-trash 
    is #in *)
(from * go #out through #tiny-frosted-window to #weed-strewn-rust)
(from #weed-strewn-rust go #in through #tiny-frosted-window to *)

#cage
(name *) wire mesh cage
(container *)
(lockable *)
(* is transparent)
(openable *)
(* is locked)
(* is closed)
(descr *)
    (collect items in * to $Items)
    (if) (empty $Items)
    (then)
        The cage is (open or closed *), and empty.
    (else)
        (if) (* is open)
        (then)
            Inside the cage
        (else)
            Through the wire mesh of the cage
        (endif)
        , you see (a $Items).
    (endif)
(instead of [unlock * with #rusted-key])
    You sling the key into the lock, but as you turn it, the rusted metal snaps off inside. 
    The crumbling fragments fall to the ground as you slam your fist against the wire mesh in frustration. 
    This is not your day.
    (now) (#rusted-key is nowhere)
(instead of [attack *])
    (* is closed)
    (knock holds #strength)
    You grip the mesh in one hand and pull, and realize it's so rusted you can easily peel it back.
    The door rips free and you drop it to the floor.
    (now) (* is open)
    (now) (* is unlocked)

(instead of [attack *])
    (* is closed)
    You try to get a grip on the mesh to rip it open, but you're just not strong enough.

(instead of [attack *])
    You've already ripped it open. No point in going mental.

(instead of [close *])
    There's not enough of the door left for you to close the cage.

#blanket
(item *)
(name *) emergency blanket
(an *)
(* is #in #cage)
(after [take *])
    (#first-aid-kit is nowhere)
    As you pick up the blanket, you find an old first aid kit underneath it.
    (now) (#first-aid-kit is #in #break-room)

#tiny-frosted-window
(window *)
(name *) tiny frosted window
(tiny *)
(prevent [leave $Room $Dir])
    (from $Room go $Dir through * to $)
    It's too small to wiggle through.

#tables
(name *) rotted picnic tables
(plural *)

#crumbling-trash
(name *) crumbling trash
(some *)

#first-aid-kit
(name *) first aid kit
(item *)
(openable *)
(container *)
(* is handled)
(#bandages is #in *)
(prevent [put $ #in *])
    It's not the best place to store stuff.

#bandages
(name *) moth-eaten bandages
(plural *)
(prevent [wear *])
    You don't need them.

