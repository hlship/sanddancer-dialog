#break-room
(office interior *)
(name *) Break Room
(look *)
    Shadows and grime linger in dark corners of this dismal room with just one tiny frosted window. 
    Some rotting picnic tables strewn with layers of sand and crumbling trash are pushed against one wall.
    On the opposite wall is
    (if) (#cage is open)
    (then)
        an open
    (else)
        a closed
    (endif)
    (name #cage)
    (collect items in #cage into $Items)
    (if) (nonempty $Items)
    (then)
        , in which (is $Items) (a $Items)
    (endif)
    . A clunky old-fashioned emergency radio
    (if) (radio is operating)
    (then) hums and crackles
    (else) sits
    (endif)
    on a table.
    (par)
    Open doorways lead west and north.
(place #storage-room north of *)
(#cage/
    #tiny-frosted-window/
    #tables/
    #crumbling-trash/
    #radio
    is #in *)
(from * go #out through #tiny-frosted-window to #weed-strewn-rust)
(from #weed-strewn-rust go #in through #tiny-frosted-window to *)

#cage
(name *) (with #strength { flimsy-looking }) wire mesh cage
(container *)
(lockable *)
~(opaque *)
(openable *)
(* is locked)
(* is closed)
(descr *)
    (collect items in * into $Items)
    (if) (empty $Items)
    (then)
        The cage is (open or closed *), and empty.
    (else)
        (if) (* is open)
        (then)
            Inside the cage
        (else)
            Through the wire mesh of the cage
        (endif)
        , you see (a $Items).
    (endif)

(prevent [unlock * with #rusted-key])
    You sling the key into the lock, but as you turn it, the rusted metal snaps off inside. 
    The crumbling fragments fall to the ground as you slam your fist against the wire mesh in frustration. 
    This is not your day.
    (now) (#rusted-key is nowhere)
(prevent [attack *])
    (* is closed)
    (knock holds #strength)
    You grip the mesh in one hand and pull, and realize it's so rusted you can easily peel it back.
    The door rips free and you drop it to the floor.
    (now) (* is open)
    (now) (* is unlocked)

(prevent [attack *])
    (* is closed)
    You try to get a grip on the mesh to rip it open, but you're just not strong enough.

(prevent [attack *])
    You've already ripped it open. No point in going mental.

(prevent [close *])
    There's not enough of the door left for you to close the cage.

#blanket
(item *)
(name *) emergency blanket
(an *)
(* is #in #cage)
~(refuse [feel *])
(feel *)
    (* is pristine)
    (#cage is closed)
    You can touch the warm wool through the mesh, but you can't get it out.
(feel *)
    The wool of the blanket is warm and comforting on this frigid night.
(when * is out of reach)
    (* is pristine)
    (#cage is closed)
    You can reach through the bars of the cage and just barely touch (the *), but that's it.

#tiny-frosted-window
(window *)
(name *) tiny frosted window
(descr *)
    (* is open)
    You've smashed the tiny frosted window open.
(descr *)
    It's a tiny window of frosted glass; makes you wonder why anyone would go to the bother.
(prevent [leave $Room $Dir])
    (from $Room go $Dir through * to $)
    It's too small to wiggle through.

#tables
(name *) rotted picnic tables
(plural *)

#crumbling-trash
(name *) crumbling trash
(some *)

#radio
(switchable *)
(tunable *)
(name *) emergency radio
(dict *) old big fifties knob switch
(descr *)
    Vintage, man. A chrome switch on the side to turn it on and off, and a big fifties
    dial on the front
    (if) (* is tuned to $Freq)
    (then)
        is tuned to (format $Freq)
    (endif)
    .
    (if) (radio is operating)
    (then)
        It's lit up from inside (space) ... are those freakin' (italic)vacuum tubes(roman)?
    (endif)
(radio is properly tuned)
    (* is tuned to $Freq)
    (deconstruct frequency $Freq into 102 3)
(radio is operating)
    (* is on)
    (#emergency-lights is on)
(prevent [switch on *])
    (#emergency-lights is off)
    You flick the switch on and off a few times, but nothing happens.
(narrate switching on *)
    With a hiss and a crackle, the old radio comes to life.
(prevent [tune * to $])
    ~(radio is operating)
    You really should get the radio working first.
(perform [listen to *])
    (radio is operating)
    Hiss and crackle.

(before [talk to #radio])
    (#radio is off)
    (first try [switch on #radio])

(prevent [talk to #radio])
    You try to raise somone, but there's no response
    (if) (#radio is off)
    (then)
        , at least on this frequency
    (endif)
    .
