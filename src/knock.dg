(current player #knock)
(#cigarettes/#lighter is #heldby #knock)

%% A little utility

@(knock holds $Object)
    ($Object is #heldby #knock)

#cigarettes
(item *)
(name *) pack of cigarettes

#lighter
(item *)
(name *) lighter
(descr *)
    (if) *(adjacent room $Room)
         ~($Room is visited)
    (then)
        You'll want a smoke before long, but for now you're itching to
        see what else is around.
    (else)
        Now that you've got your bearings a little, a smoke does sound nice.
    (endif)
(your *)

#jacket
(wearable *)
(name *) denim jacket
(your *)
(* is #wornby #knock)

%% Knock has plans

#staying-the-night
(plan *)
(name *) staying the night
(* requires #emergency-blanket/#canned-oranges)

#fixing-the-truck
(plan *)
(name *) fixing the truck
(* requires #duct-tape/#gas-can)

%% The beauty/danger of Dialog is that the required objects now exist, waiting to be
%% fully fleshed out later.

(handled plan items $Items)
    (collect $Item)
        *(plan $Plan)
        *($Plan requires $Item)
        ($Item is handled)
    (into $Items)

(in progress plans $Plans)
    (collect $Plan)
        *(plan $Plan)
        *($Plan requires $Item)
        ($Item is handled)
    (into $Plans)