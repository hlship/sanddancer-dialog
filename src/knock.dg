#knock
(current player *)
(#lighter/
 #wallet
 is #heldby *)
(* is #in #pickup-truck)
(descr *)
    You don't want to think about what you look like right now, especially
    when there's a totally legit likeness on the driver's license in your
    wallet.

(on every tick)
    (random chance of 1 in 40)
    ~{ *($Scene is happening) (dramatic $Scene) }
    ~(#coyotes-offer has completed)
    (par)
    (select)
        You shiver in the cold
    (or)
        The cold night air swirls around you
        (if) (#jacket is #wornby #knock) 
        (then)
            , and you pull your jacket closer
        (else)
            , and you regret taking off your jacket
        (endif)
    (or)
        The air is frigid, and turns your breath into clouds
    (at random)
    .

%% A little utility

@(knock holds $Object)
    ($Object is #heldby #knock)

%% Compact conditional for holding items, talents, etc.
(with $Obj $Closure)
  (if) (knock holds $Obj)
  (then)
    (query $Closure)
  (endif)


#lighter
(item *)
(name *) lighter
(descr *)
    (if) *(adjacent room $Room)
         ~($Room is visited)
    (then)
        You'll want a smoke before long, but for now you're itching to
        see what else is around.
    (else)
        Now that you've got your bearings a little, a smoke does sound nice.
    (endif)
(your *)

#jacket
(wearable *)
(name *) denim jacket
(your *)
(* is #wornby #knock)

#wallet
(item *)
(name *) wallet
(your *)
(descr *)
    Your beat-up wallet. 
    (if) (* is closed)
    (then)
        It is closed.
    (else)
        (list objects #in *)
    (endif)
(container *)
(openable *)
(#license/#receipt/#photo is #in *)
(prevent [put (hefty $Item) #in *])
    (The $Item) won't fit in your wallet.

#license
(item *)
(name *) driver's license
(your *)
(descr *)
    When the highway patrol pulls you over they see your kind of crazy uncombed hair \(morning\), your brown name \(Nakaibito Morales\)
    and brown skin \(actually more kinda tan\), your D.O.B. \(eighteen years ago just barely\) and tribal affiliation card \(expired\)
    and assume you're either some kind of native eco-terrorist, illegal drug-running border jumper, or delinquent high school dropout
    \(all lies except for kinda maybe the last one a little\). 
    Unless it's Jimmy Kay who pulled you over since he usually just wants to buy you a beer and talk about your dad, which is weird but better
    than getting a ticket.

#receipt
(item *)
(name *) receipt from Big Jimmy's

#photo
(item *)
(name *) photo
(dict *) photograph pic picture folded unfolded
(openable *)
(#ultrasound is #in #photo)
(descr *)
    (* is closed)
    You can't deal with that right now.
    Maybe after you've spent more time brooding.
(descr *)
    It's an ultrasound of Ocean's baby.
(understand [fold | $Words] as [close *])
    *(understand $Words as single object *)
(understand [unfold | $Words] as [open *])
    *(understand $Words as single object *)
(a *)
    a
    (if) (* is closed) (then) folded  (endif)
    photo
(instead of [open *])
    (count of memories into $Count)
    ($Count < 4)
    (try [examine *])
(narrate opening *)
    You've brooded enough. You unfold the photo revealing the ultrasound of Ocean's baby. Your baby.
(narrate closing *)
    You fold the photo.
(prevent [take $Item])
    ($Item is #in *)
    It's just a photo.
 
#ultrasound
(name *) ultrasound
(dict *) baby ultra sound
(an *)
(item *)
(descr *)
    Your baby too.

