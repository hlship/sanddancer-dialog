#staging-area
(office interior *)
(name *) Staging Area
(look *)
    (if) (* is dim)
    (then)
        Faint shafts of light from your dimming headlights seep through
        (if) (#pane-of-cracked-glass is open)
        (then)
            the broken window leading outside
        (else)
            a boarded-up pane of cracked glass
        (endif)
        , but you can barely make out anything of the interior.
    (else)
        It's obvious this place has been abandoned for years.
        Cold night air breathes through holes in the roof
        and everything is strewn with sand and patches of mold.
        An imposing metal desk extends out from one wall.
        The floor has half-collapsed near one corner
        (if) (#duct-tape is #in #hole)
        (then)
            , revealing a hole criss-crossed with cobwebs
        (endif)
        .
        (par)
        (if) ~(* is dim)
        (then)
            (if) (#pane-of-cracked-glass is open)
            (then)
                You can squeeze through the broken window to the outside.
            (else)
                A pane of cracked glass, opaque with dirt, is on the outside wall.
            (endif)
        (endif)
        Other rooms lie north and east.
    (endif)
(place #office north of *)
(place #break-room east of *)
(#shafts-of-light/
    #holes-in-roof/
    #patches-of-mold/
    #hole/
    #metal-desk
    is #in *)
(* is dim)
    (lit by headlights *)
    ~(lit by flashlight *)
(appearance $ #in *)
    (#emergency-lights is off)
    (#flashlight is off)
    (just) (fail)

%% TODO: You can interact too much with the items here while it is still dim
%% and those items are as yet undescribed.

%% With only the headlights for light, it's too dark to see everything. Any attempt to 
%% move to another office interior room (or failure to move from #staging-area) triggers
%% a hint about the metal desk.  On the other hand, #out to #crumbling-concrete just works.
(prevent [leave * $Direction])
    ~(lit by flashlight *)
    (lit by headlights *)
    ~{
        (from * go $Direction to room $TargetRoom)
        ~(office interior $TargetRoom)
      }
    You take a step and crack your shin against what feels like a metal desk.

(instead of [examine $])
    (current room *)
    (* is dim)
    You can hardly see anything in here; touch is about the only way
    you could navigate.

(instead of [feel #metal-desk])
    (* is dim)
    You feel around the edge of the desk, and find that it has
    a drawer.

(narrate opening #drawer)
    (* is dim)
    (#flashlight is #in #drawer)
    You slide the drawer open by feel in the darkness.
    Something large and lumpy rolls around inside -- a flashlight?

(instead of [open #metal-desk])
    (try [open #drawer])

#shafts-of-light
(name *) shafts of light
(plural *)

#holes-in-roof
(name *) holes in roof
(plural *)

#patches-of-mold
(name *) patches of mold
(plural *)

#hole
(name *) hole
(container *)
(#cobwebs/#duct-tape is #in *)
(descr *)
    (collect items in * to $Items)
    (if) (empty $Items)
    (then)
        The hole is empty
        (if) (#cobwebs is #in *)
        (then)
            , save for cobwebs
        (endif)
        .
    (else)
        Through a mass of cobwebs, you can see (a $Items) down in the whole.
    (endif)

(instead of [take $Item])
    ($Item is #in *)
    ~(knock holds #courage)
    (select)
        You reach your hand towards the hole, then pull back with a start as you brush cobwebs.
        Nightmare visions of fat black spiders shudder through your brain.
        No way are you reaching in there.
    (or)
        Yeah, sorry but you're not putting your hand in there.
    (stopping)

(prevent [put $Obj #in *])
    Sounds like a good way to lose something forever.

#cobwebs
(name *) cobwebs
(plural *)
(dict *) web spiderweb spiderwebs cobweb spider spiders webs
(fine where it is *)

#duct-tape
(item *)
(name *) roll of duct-tape
(dict *) duct tape
(narrate taking *)
    (* is #in #hole)
    You reach down into the hole, brushing cobwebs away impatiently, and pull out the duct tape.
    (now) (#cobwebs is nowhere)

#metal-desk
(name *) metal desk
(#drawer is #partof *)

#drawer
(name *) drawer
(openable *)
(#flashlight is #in *)

#flashlight
(item *)
(name *) flashlight
(dict *) light
(switchable *)
(descr *)
    Sturdy and heavy
    (if) (* is on)
    (then)
        , currently turned on
    (endif)
    .
(* provides light)
    (* is on)

(narrate taking *)
    (current room #staging-area)
    (lit by headlights #staging-area)
    Yeah, it's a flashlight all right. 
    You grip it in sudden relief, turning it in your hands until your fingers find the switch.

(narrate switching on *)
    %% TODO: This may not be accurate in a brightly lit room.
    Your surroundings spring into brightness around you.

(after [switch on *])
    (current room $Room)
    ~(brightly lit $Room)
    (try [look])




    