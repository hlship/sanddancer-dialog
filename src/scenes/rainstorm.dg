#rainstorm
(scene *)
(start *)
    (#rabbits-offer is complete)
    %% Wait until after "look up" ...
    ~(current room #burrow)
    Rain starts to come down, pattering on the desert sand. 
    Soon it's gonna be a muddy deathtrap out here.
(complete *)
    (#control-center is visited)
    ~(current room #roof)
    { (random chance of 1 in 4) (or) (force rainstorm complete) }
    *(in progress plans $)
    (par)
    The rain's dying down, the sounds of the storm fading, water sinking into the thirsty sand
    leaving only the smell of sage behind.
    (now) (#rain is nowhere)
(on every tick during *)
    (current room $Room)
    (if) (office interior $Room)
    (then)
        (random chance of 1 in 3)
        (par)
        (select)
            The building groans as wind and rain lash it from outside
        (or)
            Water from the storm trickles down from unseen cracks and holes
        (or)
            Rain beats constantly on the roof, loud and furious
        (at random)
    (else)
        (par)
        Rain batters down on the desert.
    (endif)

(prevent [listen])
    (* in progress)
    You can't hear anything above the pounding noise of water slapping sand.


#rain
(name *) rain
(dict *) storm rainstorm
(descr *)
    The frigid rain washes down on you.

($Room attracts *)
    (#rainstorm in progress)
    (around the tower $Room)

#sage
(name *) sage
((around the tower $) attracts *)
    (#rainstorm has completed)
(perform [smell *])
    The soaking rain has brought out the smell of sage throughout the desert.
    When you were a kid, it smelt like camping and adventure.
    Now it just smells like desert and confusion.
(instead of [smell])
    (player can see *)
    (try [smell *])

(end rainstorm)
    (now) (force rainstorm complete)