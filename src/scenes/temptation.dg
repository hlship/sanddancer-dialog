#temptation
(scene *)
(start *)
    (#rainstorm has completed)
    (current room $Room)
    ~(office interior $Room)
    (#coyote price can be met)
    ~($Room is one of [#roof #control-center])
    (now) (#distant-figure is #in $Room)
    (par)
    You stop in mid-stride as your eyes flick to the horizon.
    South-east of you, out in the desert, silhouetted on a rise, a distant figure
    stands.  As you watch he raises a hand and waves.
(on every tick during *)
    (random chance of 1 in 4)
    (current room $Room)
    ~(office interior $Room)
    The distant figure waves again, out in the deep desert to the south-east.
(complete *)
    (current room #open-desert)
    (now) (#distant-figure is nowhere)

#distant-figure
(male *)
(name *) distant figure
($Room attracts *)
    (#temptation in progress)
    (current room $Room)
    (around the tower $Room)
(instead of [wave])
    The figure waves back, then seems to beckon you forward.
(prevent [$ | $Stuff])
    (* is one of $Stuff)
    He's too far away. You can barely see him.
