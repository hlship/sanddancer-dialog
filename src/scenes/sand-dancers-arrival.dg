#sand-dancers-arrival
(scene *)
(dramatic *)
(start *)
    (photo has been opened)
    (tried to leave)
    There's like this faint tickle through the soles of your feet, some vibration, almost like whispers in the sand. 
    You want to bend down, put your ear to the ground, listen forever.
    (par)
    And then the ground drops out from under you, heaves, and drops again. 
    Struts groan and boards snap and then with a whooshing bang sand starts pouring in through the window and every crack, jets of sand,
    and already it's shin-deep and holy christ you've got to get out of here.
(complete *)
    (current room #roof)
    
(look (office interior $))
    (* in progress)
    (select)
        Waves of sand flow like water through the room, filling it higher and higher
    (or)
        Knee-deep sand fills the room, rising at a terrifying rate
    (or)
        The deluge of sand thunders in from every side, filling the room
    (stopping)
    .
    (current room $Room)
    (collect $Dir)
        *(from $Room go $Dir to $)
    (into $ExitDirections) 
    You glance wildly at the exits (the $ExitDirections).

(prevent $Action)
    (* in progress)
    ~{
        ($Action = [go $])
        (or)
        ($Action = [leave $ $])
        (or)
        ($Action = [look])
    }
    No time for that, you've got to escape the sand before you are buried!

(on every tick during *)
    (par)
    The building shakes and groans as sand continues to pour in, rising higher and higher.

(narrate leaving $ $)
    (* is happening)
    You wade through the sand, which is rising all the time.
    (par)
    (fail) %% Continue to default


(time for arrival)
    %% There's a lot of processing to figure out when to start, so we short circuit
    %% until after coyotes offer.
    (#coyotes-offer has completed)
    %% Note that this can only first evaluate to true while *inside* the building (that's where
    %% all plan required items are).)
    { (one complete plan) (or) (two in progress plans) }

(one complete plan)
    (complete plans $Plans)
    (length of $Plans into $Count)
    ($Count > 0)

(two in progress plans)
    (in progress plans $Plans)
    (length of $Plans into $Count)
    ($Count > 1)

(sufficient memories for sand-dancer)
    (count of memories into $Count)
    %% He wants at least 3, or all memories for "freedom"
    ($Count > 2)

(when sand starts to come)
    (time for arrival)
    (sufficient memories for sand-dancer)
    %% TODO: Feels like this could trigger when outside, which would be weird.
    (select)
        You start to go out, but then you stop. 
        Something's not right. Something's... weird.
    (or)
        The building's sinking into the sand! You've got to get out some other way!
    (stopping)
    (now) (tried to leave)

(prevent [leave #staging-area #southeast/#out])
    (when sand starts to come)

(narrate leaving #storage-room #up)
    (* in progress)
    Barely able to reach the ladder through sand up to your waist, you haul yourself up
    the rungs and pull free just in time.

(narrate leaving $ $Direction)
    (* in progress)
    You
    (select)
        climb (name $Direction) over the drifts of sand
    (or)
        wade exhaustedly through rivers of sand, all but swimming (name $Direction)
    (or)
        struggle (name $Direction)-ward against a wall of sand
    (or)
        crawl (name $Direction) in the narrow space between the sand and the roof
    (stopping)
    .
