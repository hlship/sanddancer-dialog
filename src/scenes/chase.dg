#chase
(scene *)
(dramatic *)
(start *)
    (#temptation has completed)
    %% Can only occur in open desert
    (now) (#snarling-shadows is #in #open-desert)
    Something snarls off in the distance, a warning growl.
(complete *)
    (caught by shadows)
    (now) (#snarling-shadows is nowhere)
    (par)
    It's over. They've caught you. They surround you, circling, growling, looking
    for weakness, moving in for the kill.
(on every tick during *)
    (par)
    (select)
        A howl from behind you, and an answer from somewhere ahead.
        Coyotes. Hungry, sounds like.
    (or)
        Coyotes call all around you now, circling in the dark, unseen, but coming closer, closer.
    (or)
        You can smell them now, wild, feral.
        Shifting shadows whirl around you, panting, whining with the thrill of the chase.
    (or)
        The pack's at your heels, snapping, growling, and 
        (if) (knock holds #courage)
        (then) 
            you scan the terrain looking for some advantage in making your stand
        (else)
            you've never in your life been this terrified
        (endif)
        .
    (or)
        Snarls and teeth and mangy fur on every side.
        (now) (caught by shadows)
    (stopping)
(narrate leaving $ $)
    (* in progress)
    (select)
        You move nervously forward.
    (or)
        You jog forward, eyes snapping left and right at the moving shadows around you.
    (or)
        You're running now, jumping over sagebrush and stumbling on rocks, as snarling animal forms close in around you.
    (or)
        You run.
    (stopping)

#snarling-shadows
(name *) snarling shadows
(dict *)
    coyote coyotes moving shadow animal form forms snarl growl
    sound sounds wolf call feral wild shifting pack snarls teeth mangy fur
(prevent [$ | $Words])
    (* is one of $Words)
    All you can do is run.

