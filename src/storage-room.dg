#storage-room
(office interior *)
(name *) Storage Room
(look *)
    The walls are lined with bare metal shelves,
    (if) (lit by flashlight *)
    (then)
        casting strange patterns of shadow as you sweep your flashlight beam around
    (else)
            rusting away
    (endif)
    .
    Mounted to the wall is a control panel for the building's emergency lights.
    They are switched
    (if) (#emergency-lights is on)
    (then)
        on
    (else)
        off
    (endif).
    On the shelving are hundreds of cans of food, the labels long since aged away.
    The break room lies back to the south and a utility ladder climbs through a
    broken skylight to the roof.

(place #roof up from *)
(#emergency-lights/
    #shelves/
    #ladder/
    #skylight/
    #panel 
    is #in *)

#emergency-lights
(name *) emergency lights
(dict *) bare bulbs control switch
(plural *)
(switchable *)
(narrate switching on *)
    With a sharp buzz, a wiff of static, and an electrical groan,
    bare light bulbs flicker on throughout the building.
(narrate switching off *)
    (if) (#headlights is on)
    (then)
        The lights flicker and die out.
    (elseif) (player can see #flashlight)
             (#flashlight is on)
    (then)
        The lights die out, leaving only the beam of your flashlight.
    (else)
        The lights flicker and die out
        (with #courage { but you are not afraid })
        .
    (endif)
    (line)
    The generator whines as it powers off, slowly falling silent.

#panel
(name *) electrical panel
(descr *)
    An industrial electrical panel containing the switch for the emergency lights.
(instead of [switch on *])
    (try [switch on #emergency-lights])
(instead of [switch off *])
    (try [switch off #emergency-lights])

#shelves
(name *) rows of crumbling shelving
(dict *) shelves shelf
(supporter *)
(#cans-of-food is #on *)
(descr *)
    Hundreds of cans line the bare, dusty metal shelves.
(prevent [put $ #on *])
    It feels like putting even one more thing on the shelves could cause a huge collapse.
(prevent [climb *])
    The shelves are too narrow and rickety to support your weight.

#ladder
(name *) utility ladder
(instead of [enter/climb #ladder])
    (try [go #up])

#skylight
(name *) broken skylight
(out of reach *)

#cans-of-food
(item *)
(name *) cans of food
(dict *) hundreds can
(prevent [take *])
    (#canned-oranges is handled)
    You won't be hungry again till morning.
(descr *)
    There are hundreds of cans, all give or take the same size, and the labels have long since worn away.
(instead of [take *])
    ~(#canned-oranges is handled)
    (knock holds #can-opener)
    (knock holds #luck)
    You run your fingers along the cans, waiting until one feels right.
    There's nothing, nothing, but then... yeah.
    That's the one.
    You snag it down, twist it open, and boom: mandarin oranges, unspoiled and delicious.
    You scarf them down, picking them out segment by segment with your chilly fingers.
    By the time you've polished them off you're a lot less hungry. 
    You grab the next can down for later.
    (now) (knock holds #canned-oranges)
    %% We're responsible for this; normally the responsibility of [take $] action:
    (now) (#canned-oranges is handled)
    (now) (#orange-smell is #in #storage-room )
(prevent [take *])
    (knock holds #can-opener)
    You pull down a can from the shelf, grip it with the rusty can opener, and peel open the lid.
    Yuck:
    (select)
        moldy peas
    (or)
        rotten condensed milk
    (or)
        black and mold-covered mushrooms
    (or)
        something nasty-smelling and green
    (at random) %% Dialog doesn't have "as decreasingly likely outcomes"
    .
    (select)
        Just your luck.
    (or)
        Not your lucky day.
    (or)
        Some of these should still be good, right?
    (at random)
(prevent [take *])
    You grab one and turn it over and over in your hand, but water has soaked and crumbled away the label,
    and you don't have any way to get it open.
    Your stomach growls a little as you put it back on the shelf.
(instead of [open/eat *])
    (try [take *])

#canned-oranges
(item *)
(name *) canned oranges
(plural *)

#orange-smell
(name *) lingering smell of orange
