#storage-room
(office interior *)
(name *) Storage Room
(look *)
    The walls are lined with bare metal shelves,
    (if) (lit by flashlight *)
    (then)
        casting strange patterns of shadow as you sweep your flashlight beam around
    (else)
            rusting away
    (endif)
    .
    Mounted to the wall is a control panel for the building's emergency lights.
    They are switched
    (if) (#emergency-lights is on)
    (then)
        on
    (else)
        off
    (endif).
    The break room lies back to the south and a utility ladder climbs through a
    broken skylight to the roof.

(place #roof up from *)
(#emergency-lights/
    #shelves/
    #ladder/
    #skylight 
    is #in *)

#emergency-lights
(name *) emergency lights
(plural *)
(switchable *)
(narrate switching on *)
    With a sharp buzz, a wiff of static, and an electrical groan,
    bare light bulbs flicker on throughout the building.
(narrate switching off *)
    The lights flicker and die out.

#shelves
(name *) bare metal shelves

#ladder
(name *) utility ladder

#skylight
(name *) broken skylight
(out of reach *)
