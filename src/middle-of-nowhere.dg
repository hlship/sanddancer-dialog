#middle-of-nowhere
(around the tower *)
(name *) Middle of Nowhere
(look *)
    The tire tracks from the south stop abruptly here, but where the hell
    are you? The desert sand and clumps of pale sagebrush are all 
    (if) (lit by headlights *)
    (then)
        your dimming headlights pick out before barely reaching
    (elseif) (lit by flashlight *)
    (then)
        your flashlight picks out, other than
    (else)
        you can see in the glow of
    (endif)
    the concrete building to the north.
    (par)
    Your poor old pickup truck ticks and groans, smashed gracelessly against 
    a tall Saguaro; 
    (if) (knock holds #scent)
    (then)
        the overpowering stench of gasoline permeates the air
    (else)
        wiffs of evaporating gasoline linger in the chill air
    (endif)
    .
    (descr truck dash)
    (if) (#knock is #in #pickup-truck)
    (then)
        The glove compartment is (open or closed #glove-compartment).
    (endif)
    (if) (#lizard is #in *)
    (then)
        (par)
        (if) (#knock is #in #pickup-truck)
        (then)
            Through the windshield, you see a little lizard clinging frantically to the Saguaro.
        (else)
            A brown-colored lizard clings to the cactus.
        (endif)
    (endif)
(#knock/
    #pickup-truck/
    #wiffs-of-gasoline/
    #saguaro/
    #lizard
    is #in *)
(place #backtracking south of *)
(place #crumbling-concrete north of *)

(descr truck dash)
    (collect items in #pickup-truck into $Items)
    (if) ~(empty $Items)
    (then)
        On the dash you can see (a $Items).
    (endif)

#pickup-truck
(name *) pickup truck
(dict *) car door dash old poor
(openable *)
(* is transparent) %% Can see through the window, lets light in too.
(actor container *)
(#jade is #in *)
(before [enter/leave *])
    (* is closed)
    (try [open *])
(narrate opening *)
    You work the rusty truck door open.
(narrate closing *)
    You swing the truck door shut with a squeal of rusty hinges.
(perform [examine *])
    Your truck is (open or closed *).
    (descr truck dash)
%% This prevents a redundant list of items in the truck, when Knock looks while also in the truck:
~(appearance $ $ *)
(prevent [take $Obj])
    ($Obj is #in *)
    ~(#knock is #in *)
    You can't reach (the $Obj) from outside the truck.
(prevent [put $ #in * ])
    ~(#knock is #in *)
    You need to be inside the truck to do that.
    
%% Players really want to just drive the truck away.

(understand [drive/shift/steer/brake/start] as [drive])

(understand [drive/start | $Words] as [drive])
    *(understand $Words as single object *)

(understand [turn on | $Words] as [drive])
    *(understand $Words as single object *)

(understand [turn key] as [drive])
    (player can see *)
    %% TODO: Watch out for if knock has rusty key

(prevent [drive])
    ~(player can see *)
    You don't see any kind of vehicle here.

(prevent [drive])
    ~(#knock is #in *)
    It's generally safer to drive from inside the vehicle, but in this case
    it doesn't matter -- your pickup truck is in no shape to go anywhere.

(prevent [drive])
    Your pickup truck is in no shape to go anywhere until you fix
    that leaking gas line. Even then you'd be lucky to get it moving or find
    the highway.

#glove-compartment
(container *)
(name *) glove compartment
(dict *) glovebox box
(openable *)
(descr *)
    (if) (* is closed)
    (then)
        The glove compartment is closed.
    (elseif) ($ is #in *)
    (then)
        (list objects #in *)
    (else)
        The glove compartment is open and empty.
    (endif)
~(appearance $ $ *)
(* is #partof #pickup-truck)
(prevent [open/close *])
    ~(#knock is #in #pickup-truck)
    You can't reach the glove compartment from outside the truck.

(global variable (remaining cigarettes 6))

#pack
(item *)
(name *) pack of cigarettes
(dict *) cig cigarette cigs smokes
(descr *)
    (remaining cigarettes $Count)
    Your pack of cigarettes
    (if) ($Count = 0)
    (then)
        , now empty
    (else)
        \( (spell out $Count) remaining \)
    (endif)
    .
(* is #in #glove-compartment)
(instead of [smoke *])
    *(scene $Scene)
    ($Scene is happening)
    (dramatic $Scene)
    This isn't really the best time for that, man.

#headlights
(name *) headlights
(dict *) light head headlight lights
(plural *)
(switchable *)
(* is on)
(* is #partof #pickup-truck)
(prevent [switch on/off *])
    ~(#knock is #in #pickup-truck)
    You can't operate the headlights from outside your pickup truck.
(narrate switching on *)
    The desert springs into existence around you.
(narrate switching off *)
    You click the headlights off
    (if) (#emergency-lights is off) (#flashlight is off)
    (then)
        and blackness swallows you up
    (elseif) (#emergency-lights is off) 
             (#flashlight is on)
             (player can see #flashlight)
    (then)
        leaving only the ghostly beam of your flashlight
    (endif)
    .
%% Allow Knock to turn the headlights back on "by feel"; otherwise
%% you can turn them off and not get them back on again.
(add * to scope)
    (#knock is #in #pickup-truck)

#wiffs-of-gasoline
(name *) wiffs of gasoline
(dict *) gas wiff
(plural *)
(prevent [$Verb *])
    ~($Verb is one of [smell take])
    Fading away in the chill night air, but enough to tell you something
    on your truck's busted bad.

#saguaro
(plant *)
(name *) tall Saguaro
(dict *) cactus

#jade
(name *) (with #luck { lucky }) piece of jade
(item *)
(descr *)
    Grandma said it will bring you luck, which hasn't really been working out.
    You can't remember now how it ended up in your pickup truck.
    Maybe someday you'll see someone lucky and you can throw it at him.

#lizard
(name *) brown lizard
(descr *)
    (select)
        He looks pretty pissed that you crashed into his cactus.
        What did grandma used to call those little dudes?
        You can't remember any more.
    (or)
        Oh yeah, you remember, she called them sand-dancers.
        This little sand-dancer still looks pissed.
    (or)
        The little sand-dancer lizard stares at you with one slitted eye.
    (stopping)
(instead of [give $Obj to *])
    You dangle (the $Obj) in front of it, but it just flicks it tongue in and out a little
    faster, staying put.
(instead of [$Verb | $Words])
    ($Verb is one of [attack take push touch])
    (* is one of $Words)
    You reach out to grab it, but in a flash, it scurries around to the other size of the cactus and disappears.
    (now) (* is nowhere)

