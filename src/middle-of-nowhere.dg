#middle-of-nowhere
(around the tower *)
(name *) Middle of Nowhere
(look *)
    The tire tracks from the south stop abruptly here, but where the hell
    are you? The desert sand and clumps of pale sagebrush are all 
    (if) (lit by headlights *)
    (then)
        your dimming headlights pick out before barely reaching
    (elseif) (lit by flashlight *)
    (then)
        your flashlight picks out, other than
    (else)
        you can see in the glow of
    (endif)
    the concrete building to the north.
    (par)
    Your poor old pickup truck ticks and groans, smashed gracelessly against 
    a tall Saguaro; wiffs of evaporating gasoline linger in the chill air.
    (descr truck dash)
(#knock/
    #pickup-truck/
    #wiffs-of-gasoline/
    #saguaro 
    is #in *)
(place #backtracking south of *)
(place #crumbling-concrete north of *)

(descr truck dash)
    (collect items in #pickup-truck to $Items)
    (if) ~(empty $Items)
    (then)
        On the dash you can see (a $Items).
    (endif)

#pickup-truck
(name *) pickup truck
(openable *)
(actor container *)
(before [enter/leave *])
    (* is closed)
    (try [open *])
(narrate opening *)
    You work the rusty truck door open.
(perform [examine *])
    Your truck is (open or closed *).  (descr truck dash)
%% This prevents a redundant list of items in the truck, when Knock looks while also in the truck:
(appearance $ #in *)
    (just) (fail)
    
#headlights
(name *) headlights
(dict *) light head
(plural *)
(switchable *)
(* is on)
(* is #partof #pickup-truck)
(narrate switching on *)
    The desert springs into existence around you.
(narrate switching off *)
    You click the headlights off
    (if) (#emergency-lights is off) (#flashlight is off)
    (then)
        and blackness swalls you up
    (elseif) (#emergency-lights is off) 
             (#flashlight is on)
             (player can see #flashlight)
    (then)
        leaving only the ghostly beam of your flashlight
    (endif)
    .

#wiffs-of-gasoline
(name *) wiffs of gasoline
(dict *) gas wiff
(plural *)
(prevent [$Verb *])
    ~($Verb is one of [smell take])
    Fading away in the chill night air, but enough to tell you something
    on your truck's busted bad.

#saguaro
(plant *)
(name *) tall Saguaro
(dict *) cactus

%% Bonus room

#backtracking
(around the tower *)
(name *) Backtracking
(look *)
    It's quieter and darker over here, the black desert night bleed in.
    Your tire track are hardly visible in the desert sand, and you can barely make
    out the way north back to your truck.
(#withered-cactus is #in *)

#withered-cactus
(plant *)
(* is dead)
(name *) withered cactus
