
%% office interior region

#staging-area
(office interior *)
(name *) Staging Area
(link * #north to #foremans-office)
(link * #east to #break-room)

#foremans-office
(office interior *)
(name *) Foreman's Office
(look *)
    This must have been the foreman's office for whatever this building was about.
    A half collapsed desk sits against a wall.
    (if) (#flashlight is pristine)
    (then)
        Mixed among the chaos is an old flashlight.
    (endif)
    %% TODO: list anything visible on the desk
(#desk/#flashlight is #in *)

#break-room
(office interior *)
(name *) Break Room
(link * #north to #storage-room)

#storage-room
(office interior *)
(name *) Storage Room
(look *)
    A storage room adjacent to the break room to the south.
    Steps lead up to the building's roof.
    A bulky box is attached to one wall; this seems to be the emergency lighting system.
(link * #up to #roof)
(#emergency-lights is #in *)

#roof
(around the tower *)
(name *) Roof

%% Objects

#desk
(supporter *)
(name *) half-collapsed desk

#key
(item *)
(name *) rusted key
(* is #on #desk)

#flashlight
(item *)
(name *) flashlight
(dict *) light
(switchable *)
(descr *)
    Sturdy and heavy
    (if) (* is on)
    (then)
        , currently turned on
    (endif)
    .

#emergency-lights
(name *) emergency lights
(switchable *)


#cage
(item *)
(name *) wire mesh cage
(container *)
(openable *)
(fine where it is *)
(* is closed)
(* is #in #break-room)

#blanket
(item *)
(name *) emergency blanket
(* is #in #cage)

#wastepaper-basket
(item *)
(container *)
(name *) wastepaper basket
(* is open) 
(fine where it is *)
(* is #in #foremans-office)

%% Windows

#pane-of-cracked-glass
(window *)
(name *) pane of cracked glass
(dict *) window
(* is #in #staging-area)

#dust-covered-window
(window *)
(name *) dust covered window
(* is #in #foremans-office)

#tiny-frosted-window
(window *)
(* is open)
~(openable *)
(name *) tiny frosted window
(* is #in #break-room)

%% All windows are openable, but start closed.
%% This has to come *after* the above, to ensure that
%% individual windows can override it, like the tiny frosted window.

(openable *(window $))
