#office
(office interior *)
(name *) Foreman's Office
(look *)
    (if) (lit by flashlight *)
    (then)
        You sweep the beam of your flashlight around
    (else)
        The stark emergency lights illume
    (endif)
    this tiny office,
    probably once where the boss sat under a dust covered window.
    A half collapsed desk sits against a wall.
    (collect $Item)
        *($Item is #on #desk)
    (into $Items)
    (if) ~(empty $Items)
    (then)
        On the desk (is-are $Items) (a $Items).
    (endif)
    (if) (#flashlight is pristine)
    (then)
        Mixed among the chaos is an old flashlight.
    (endif)
    (par)
    The only exit is to the south.
    %% TODO: list anything visible on the desk
(#desk/
    #flashlight/
    #wastepaper-basket/
    #dust-covered-window 
    is #in *)

#desk
(supporter *)
(name *) half-collapsed desk

#key
(item *)
(name *) rusted key
(* is #on #desk)

#flashlight
(item *)
(name *) flashlight
(dict *) light
(switchable *)
(descr *)
    Sturdy and heavy
    (if) (* is on)
    (then)
        , currently turned on
    (endif)
    .
(* provides light)
    (* is on)

#wastepaper-basket
(item *)
(container *)
(name *) wastepaper basket
(* is open) 
(fine where it is *)


#dust-covered-window
(window *)
(name *) dust covered window
