%% Memories start buried but may be retrieved

@($ is buried) ~($ is retrieved)

#emotional-baggage
(item *)
(name *) emotional baggage
(descr *)
    Your guidance counselor used to say you're always carrying it with you.
    You imagine it's kind of ugly, lumpy, and green, and definitely has a stuck zipper.
    (list objects #in *)
(container *)
(* is open)
(* is #heldby #knock)
(your *)
(prevent [$Verb | $Objects])
    ~($Verb = @examine)
    (* is one of $Objects)
    It's not real, bro.

(after [inventory])
    (par)
    (list objects #in *)

%% Memories exist as almost-items, but you can't really do anything with them
(unlikely $Action)
    *($Obj is one of $Action)
    (memory $Obj)

%% Objects and items may suggest a memory

#last-day-of-high-school
(memory *)
(name *) last day of high school
(your *)
(#tumbleweed suggests *)
(descr *)
    You didn't expect it was going to be your last day.
    But that morning you got called in to the principal's office and fat bald Mr. Cox
    and pissy old Mrs. Burke were there, and they looked kind of like strong
    animals stalking weak animals, and you knew something bad was up.
    (par)
    Cox said I have here, Mr. Morales \(a bad start since you hate that name\)
    a test you took last week in Mrs. Burke's sophmore English class.
    Questions have been raised \(he looked up at Mrs. Burke
    like he was trying to pass the buck\) questions have been raised about the quality
    of your essay, and whether a student with your academic and behavioral record
    \(he scratched his bloated nose meaningfully\)
    could have plausibly produced such as essay, and you get the idea.
    They thought you cheated.
    (par)
    No. They knew you cheated, deep in their smug empty hearts.
    The wanted you to admit it, say you were a cheater and a liar.
    But you weren't.
    You wrote the essay, every god damn word, because you really liked the book for once
    and wanted to show Mrs. Burke that maybe if they gave people better books to read kids would
    actually learn something. 
    But they wanted a confession.
    They wanted a thieving example they could parade in front of the school.
    Someone of your academic and behavioral and economic and racial background and yeah,
    screw this shit.
    So you got up and left and never came back.
    Drop out, hell.
    You walked out and you'd do it again.

#your-shit-job
(memory *)
(name *) shit job
(your *)
(#receipt suggests *)
(descr *)
    Juza straddles the 371 like a drunk at last call, smelly and without a plan for the future.
    Shiny cars whiz by at ninety once in a while, only stopping for gas or directions
    \(hint: not this way\).
    No one ever stops for the stalls selling food and jewelry and blankets and cheap t-shirts that
    cluster around the dirt turn-off onto the rez.
    At least, no one buying anything.
    (par)
    Big Jimmy shook his head when he heard you lived in Oro Oeste.
    Hell of a drive, kid, he said, popping up his Lumberjacks cap to scratch the straw pate underneath.
    You should get a place in Hoo-zuh.
    Little Jimmy'll set you up in that trailer cross the road for almost nothin'.
    (par)
    Almost nothing. Yeah, exactly what you'd turn into on the cold day in hell you move to Juza.
    But on the other hand, that was before you met Ocean.
    Now the road back to Oro Oeste seems longer and longer every night.

#grandmas-stories
(memory *)
(name *) grandma's stories
(dict *) grandma story
(your *)
(#jade suggests *)
(descr *)
    There are dark spirits who roam the earth, little Knock.
    Grandma used to say that, holding you tight and stroking your hair.
    There are dark spirits who roam the earth, but you're not alone.
    Oh, no.
    I'm here.
    \(She'd kiss your head and you'd squeeze her back.\)
    But others are watching out for you too.
    You have three animal guardians, hmm?
    Spirits who are always watching over you.
    Oh, you can't always trust them to know what's best.
    Remember that, Knock.
    But when you need help, they'll come, and protect you from the worser things in the world.
    (par)
    Mom would yell at Grandma a lot for filling your head with that new-age bullshit.
    Grandma grew up white and midwestern and Baptist, but had started wearing things with feathers
    and playing the pan flute by the time your were born.
    She seemed to really like having a son-in-law who was Native American or American Indian
    or Indigenous People or whatever she'd decided the term was that week,
    and she was pretty pissed when Mom left him.
    Anyway.
    Her stories were mostly BS, you guess, but some of them stuck with you.
    When it's dark you still wonder if your spirit animals are out there, somewhere, and
    what the hell is taking them so long to find you.

#meeting-ocean
(memory *)
(name *) meeting Ocean
(proper *)
(#rusted-key suggests *)
(descr *)
    She was buying a Fresca at Big Jimmy's when you got off your shift, shoving work keys in your pocket,
    and you stood behind her waiting to get some cigs, almost too tired to notice how cute she was but still noticing,
    yeah, still noticing.
    (par)
    She turned around and caught you noticing and you were pretty embarrassed and covered in sweat and grease
    from the garage, so you stepped up and bought some Camels and were pretty surprised that she was still there
    when you turned around.
    (par)
    She told you her name was Ocean Running Deer and she lived on the rez and you told her your name
    was Nakaibito Morales and you lived in Oro Oeste.
    She said isn't Nakaibito the nae of a town off west and you told her your mom had picked it off a map
    because it sounded like a good Indian name.
    You never tell anyone that story but for some reason you told her.
    She laughed, sweetly though, and said maybe the two of you should go there sometime and see
    what is was like.
    Somehow two weeks later you were dating although you never ended up going to Nakaibito.
    (par)
    And now... no, you can't think about that yet.

#oceans-perfume
(memory *)
(name *) Ocean's perfume
(#orange-smell suggests *)
(descr *)
    You used to like it. 
    The perfume. 
    But you like how Ocean smells without it better, but she doesn't believe you.
    She says girls are supposed to smell sweet, like oranges and rose petals.
    But you've always liked bitter fruits. 
    Tamarind and lemon.
    (par)
    And sometimes, maybe after a fight or something, you'll drive off and that's all you can smell is her orange perfume. 
    And even after smoking three cigarettes with the windows of your pickup rolled down you can still smell it, and it's still too sweet, and you wonder if this is working out. 
    Because it's such a little pointless thing but it drives you crazy. 
    And if a little thing like perfume is driving you this crazy maybe the whole thing is doomed.
    Maybe Ocean would be better off with some guy who likes orange perfume.
    (par)
    You don't know what's going to happen now.

#graduation-night
(memory *)
(name *) her graduation night
(dict *) oceans ocean's
(#bow-tie suggests *)
(proper *)
(descr *)
    She grinned when you picked her up, and you drove on in to Mike's and the lot was filled with trucks and Mike's was filled with Indian guys and Indian girls and she was still the most beautiful,
    and you danced a lot and drank some and left early to go lie down under the stars and count the shooting ones.
    (par)
    And that was maybe you think the first time she said she loved you, and you said it back cause it felt all right and shit,
    but you didn't really know if you meant it, or if she did, or what it even really means. I mean you like Ocean a lot, really a lot, 
    but then you also aren't really sure you even know her, anything about her that's important or real or meaningful.
    She could be anyone, really, on the inside. So could you.

#heard-the-news
(memory *)
(name *) when you heard the news
(#ultrasound suggests *)
(proper *)
(descr *)
    Four weeks ago now, a little colder and a little darker. On a night just like this one except you didn't drive off the road and crash into a cactus,
    you got off work and drove the state highway in the dark and crossed over the edge of the res to Ocean's dad's trailer, 
    and she told you that night while you were dead exhausted, Knock, I'm pregnant.
    (par)
    And all you could think was how totally not real it seemed, after an hour of blackness and New Mexican night streaming by, bugs exploding in tiny tragedies on your windshield, 
    you just felt like driving on, like this thing was some podunk town you could breeze by, no services, ranch exit only. And you thought of every movie they ever made you watch in
    school and every lecture and every living example, the drunken uncles and snaggle-toothed coworkers who knocked up their high school sweethearts and started families on minimum 
    wage and no prospects and never went anywhere and never did anything and what did you do? How could this happen? Why were you both so stupid?
    (par)
    Ocean's aunt is paying for the baby but it's been made clear the money stops when the baby's born. Ocean wants to keep it, she's always wanted to be a mommy. But you never thought you'd be a daddy. And
    when she pushed this into your hand last night she told you, Knock, it's time to decide.
    If you aren't with me on this, if you don't want this as much as I do, then this can't go on anymore and you should go. 
    Fly. Head west. Find Nakaibito. She smiled just a little and traced your ear.
    (par)
    But decide soon, Knock. Decide soon.

#road-trips
(memory *)
(name *) road trips through the desert
(dict *) trip
(#gas-can suggests *)
(proper *)
(descr *)
    Always a faint whiff of gas in your truck, even on the highway at midnight
    with the windows cranked and scorching summer air sending Ocean's hair
    twisting and curling behind the passenger seat. The two of you driving
    through the night to visit her aunt in Santa Rosa, about a week before it
    happened, talking and laughing and sometimes her hand on your leg and
    sometimes your hand on hers, nothing alive but you and her and the truck
    and the road and the blackness beyond. 
    (par)
    Then all quick she leans forward at a green phantom roadsign and goes hey
    look, it's the road to Nakaibito! And you have no idea what she's talking
    about till you remember the story \(and that you told her the story\) and she
    says come on, let's go, let's find out what's there. And you're like what,
    now? And she laughs and says when else? And it's crazy and you tell her
    it's crazy, it'd be like a four hour drive there and four hours back,
    totally out of your way and probably for nothing, a couple trailers and
    broken glass, and you pass the exit and drive on and tease her a little but
    inside you're thinking, this is why I love her \(if I do\), that crazy, that
    what-the-hell let's do something wild and go with it spark, that's Ocean.
    That's your girl. And maybe you should have done it because now maybe your
    days of crazy gambling road trips are over, and maybe you'll never find out
    what's in Nakaibito or anywhere else.

#watching-family-guy
(memory *)
(name *) watching Family Guy with Karl
(proper *)
(#sage suggests *)
(descr *)
    Whatcha watchin, Knock? he asked, lurching out of mom's room smelling of
    sage incense and holding a beer and crashing into the lazyboy, and you said
    Nothing Mr Morales \(and god damn it kid, can't you at least call me Karl if
    you won't call me dad? but that conversation's been buried a year now at
    least out loud\) and he squinted and said is this that Family Guy crap? I
    thought your mother said she didn't want you watching this crap \(because
    it's always mother who wants you to do things or be things or say things,
    never Mr Morales\) I thought your mother said you ought to start learning
    about your culture.
    (par)
    So while Peter is trying to start a rock band or something Karl starts
    spouting some story about Mexican myths with eagles and feathered serpent
    gods, and he must be so into the stepdad thing he's forgotten that's his
    culture, not yours \(well, sort of\). Not that your real dad or Peter Griffin
    ever helped you figure out what your culture's supposed to be either. It
    hits you that your culture must be somewhere in the middle, between Family
    Guy and Quetzalcoatl, and you start laughing and get grounded because you
    can't even imagine what the middle between those two could possibly look
    like.



%% Brood verb to access memories:

%% See https://intfiction.org/t/adding-grammar-tokens/46549

(grammar [brood about/on/over [memory]] for [brood $])
(grammar [brood [memory]] for [brood $])

@(grammar transformer [[memory] | $Tail] $SoFar $Verb $Action $Rev)
	(grammar transformer $Tail [1000 | $SoFar] $Verb $Action $Rev)

(match grammar token 1000 against $Words $ into $Obj)
    *(understand $Words as single object $Obj preferably { (memory $_) })

(describe action [brood $Obj])
    brood about (the $Obj)

(prevent [brood $Obj])
    ($Obj suggests $Memory)
    It's not (the $Obj) you want to brood about, it's (name $Memory).

(prevent [brood $Obj])
    ~(memory $Obj)
    Eh. You couldn't really get into a good brood about that when it's this damn cold.

(prevent [$Verb | $Nouns])
    ~($Verb is one of [brood trade])
    *($Noun is one of $Nouns)
    (memory $Noun)
    As if. All you can really do is BROOD ABOUT it.

(perform [brood $Memory])
    (narrate brooding about $Memory)
    (now) ($Memory is retrieved)

(narrate brooding about $Memory)
    (descr $Memory)

(perform [think])
    Most of the thinking you do nowadays comes from either smoking or brooding.

%% Every turn when we see something that is linked to a memory, note it, and
%% move it to emotional baggage.

(on every tick)
    (identify recalled memories)

(identify recalled memories)
    *(memory $Memory)
    ($Memory is nowhere)
    %% Watch out for memory getting re-identified after trading (if player carries the suggesting object):
    ~($Memory is traded)
    %% For now, object <-> memory is 1:1
    ($Obj suggests $Memory)
    ~($Obj is hidden)
    (player can see $Obj)
    (par)
    Something about (the $Obj) makes you want to brood about (the $Memory).
    (now) ($Memory is #in #emotional-baggage)

%% Count up the number of retrieved memories
(count of memories into $Count)
    (accumulate 1)
        *($Memory is #in #emotional-baggage)
    (into $Count)

(instead of [wait])
    { 
        (select random item in #emotional-baggage into $Memory)
        (or)
        {
            (collect $Memory)
                *(memory $Memory) 
            (into $Memories)
            (randomly select $Memory from $Memories)
        }
    }
    Nah, man, better keep busy or else it's back to brooding about (the $Memory).